apply plugin: "java-library"

sourceCompatibility = 1.8
sourceSets.main.java.srcDirs = [ "src/" ]
sourceSets.main.resources.srcDirs = ["../assets"]

project.ext.mainClassName = "com.gempukku.gdx.assistant.DesktopLauncher"
project.ext.assetsDir = new File("../assets")

import org.gradle.internal.os.OperatingSystem

dependencies {
    implementation project(":core")
    implementation project(":gdx-plugins")
    implementation project(":gdx-assistant-plugin")

    api "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
    api "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
}

def assistantPlugins = [
        "https://jitpack.io/com/github/MarcinSc/gdx-graph/gdx-graph-assistant/7368dc7/gdx-graph-assistant-7368dc7.jar"
]

build {
    def folder = new File("plugins");
    folder.mkdirs();

    for (def assPlugin : assistantPlugins) {
        def f = new File("plugins/"+assPlugin.substring(assPlugin.lastIndexOf("/")+1));
        if (!f.exists()) {
            println("Downloading plugin: "+assPlugin);
            new URL(assPlugin).withInputStream { i -> f.withOutputStream { it << i } }
        }
    }
}

task run(dependsOn: classes, type: JavaExec) {
    mainClass = project.mainClassName
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
    workingDir = project.assetsDir
    ignoreExitValue = true

    if (OperatingSystem.current() == OperatingSystem.MAC_OS) {
        // Required to run on macOS
        jvmArgs += "-XstartOnFirstThread"
    }
}

task debug(dependsOn: classes, type: JavaExec) {
    mainClass = project.mainClassName
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
    workingDir = project.assetsDir
    ignoreExitValue = true
    debug = true
}

task dist(type: Jar) {
    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
    manifest {
        attributes 'Main-Class': project.mainClassName
    }
    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
}


dist.dependsOn classes

eclipse.project.name = appName + "-desktop"
